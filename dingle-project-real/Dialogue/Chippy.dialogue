~ start
if not StoryFlags.completedIntro
	=> intro
elif StoryFlags.completedIntro and not StoryFlags.caughtGulls
	=> postIntro
elif StoryFlags.caughtGulls
	=> allDoneAlert
elif StoryFlags.alertGiven
	=> postAlert

~ intro
???: OH GOD NOT AGAIN! 
do CutSceneManager.move_to("Chippy", "RightOfAlley")
do wait(1.0)
???: [wave]YOU GOTTA HELP ME GET ALL THE DIALOGUE CORRECT PLEASE![/wave]

- Oh boy can't wait!
	Chippy: YIPEEEE!!!
	
- Nah, I'm good!
	Chippy: Well... [speed=0.2] This is due tommorow sooooo...
	do wait(0.5)
	Chippy: You gotta do it anyway :D

do StoryFlags.completedIntro = true
=> end

~ postIntro
Chippy: Have you found the culprit yet???
=> end

~ allDoneAlert
do StoryFlags.alertGiven = true
do CutSceneManager.teleport_to("Chippy", "ChippyDocksStart")
do CutSceneManager.move_to("Chippy", "ChippyDocksEnd")
Chippy: HEY!!!
do wait(3.5)

Chippy: Do you know the culprit yet?
- Yes
	Chippy: Alright lets go resolve this!
- No
	Chippy: Alright...
	Chippy: When you think you know or run out of things to do come to my shop.
	do CutSceneManager.move_to("Chippy", "ChippyDocksStart")
	do wait(5.0)
	=> specielEnd
=> end

~ postAlert
Chippy: So you think you know the culprit???
- Yes
	Chippy: Alright lets go resolve this!
- No
	Chippy: Alright then. 
	Chippy: Just tell me when you have ok?
=> end

~ specielEnd
do CutSceneManager.teleport_to("Chippy", "ChipperEnterance")

~ end
do CutSceneManager.end()

=> END